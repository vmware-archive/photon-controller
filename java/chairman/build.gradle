apply plugin: 'application'
apply plugin: 'thrift'

mainClassName = 'com.vmware.photon.controller.chairman.Main'

dependencies {
  compile project(':common')
  compile project(':common-logging')
  compile project(':cloud-store')

  compile "com.fasterxml.jackson.core:jackson-databind:${versions.jackson_core}"
  compile "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:${versions.jackson_core}"
  compile "com.google.guava:guava:${versions.guava}"
  compile "com.google.inject.extensions:guice-assistedinject:${versions.guice}"
  compile "com.google.inject:guice:${versions.guice}"
  compile "org.hibernate:hibernate-validator:${versions.hibernate_validator}"
  compile 'net.sourceforge.argparse4j:argparse4j:0.4.0'
  compile 'org.eclipse.jetty:jetty-util:8.1.10.v20130312'

  thrift files('../../thrift/host.thrift',
      '../../thrift/chairman.thrift',
      '../../thrift/resource.thrift',
      '../../thrift/scheduler.thrift',
      '../../thrift/root_scheduler.thrift',
      '../../thrift/roles.thrift',
      '../../thrift/flavors.thrift',
      '../../thrift/tracing.thrift',
      '../../thrift/status.thrift')

  testCompile 'org.hamcrest:hamcrest-all:1.3'
  testCompile 'org.testng:testng:6.8'
  testCompile "org.powermock:powermock-api-mockito:${versions.powermock}"
  testCompile "org.powermock:powermock-module-testng:${versions.powermock}"
}

jar {
  manifest {
    attributes 'Specification-Title': 'Photon Controller Chairman',
        'Specification-Version': version,
        'Built-By': System.getProperty('user.name'),
        'Built-Date': new Date(),
        'Built-JDK': System.getProperty('java.version'),
        'Built-Gradle': gradle.gradleVersion,
        'Git-Commit': "git rev-parse --short HEAD".execute().text.trim()
  }
}

run {
  if (project.hasProperty("appArgs")) {
    args Eval.me(appArgs)
  }
}
