apply plugin: 'java'

dependencies {
  compile project(':common')
  compile project(':common-dcp')
  compile project(':cloud-store')
  compile project(':nsx-client')

  compile 'com.google.code.findbugs:jsr305:2.0.1'

  testCompile "org.hamcrest:hamcrest-all:${versions.hamcrest}"
  testCompile "org.testng:testng:${versions.testng}"
  testCompile "org.mockito:mockito-core:${versions.mockito}"
  testCompile("org.powermock:powermock-api-mockito:${versions.powermock}") {
    exclude group: 'org.mockito', module: 'mockito-all'
  }
  testCompile "org.powermock:powermock-module-testng:${versions.powermock}"
  testCompile(project(path: ':common-tests'))
}

jar {
  baseName = 'api-backend'
  manifest {
    attributes 'Specification-Title': 'Photon Controller Management API Backend',
        'Specification-Version': version,
        'Built-By': System.getProperty('user.name'),
        'Built-Date': new Date(),
        'Built-JDK': System.getProperty('java.version'),
        'Built-Gradle': gradle.gradleVersion
  }
}

tasks.withType(Test) {
  scanForTestClasses = false
  include "**/*Test.class"
  exclude "**/helpers/*.class"
}

test {
  useTestNG() {
    excludeGroups 'flaky'
  }
}
