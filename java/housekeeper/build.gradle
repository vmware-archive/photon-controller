apply plugin: 'application'
apply plugin: 'thrift'
apply plugin: 'com.bmuschko.docker-remote-api'

import com.bmuschko.gradle.docker.tasks.container.*
import com.bmuschko.gradle.docker.tasks.image.*
import java.nio.file.Files

mainClassName = 'com.vmware.photon.controller.housekeeper.Main'

dependencies {
  compile project(':common')
  compile project(':common-dcp')
  compile project(':common-logging')
  compile project(':cloud-store')
  compile project(':common-tests')

  compile "com.google.guava:guava:${versions.guava}"
  compile "com.google.inject.extensions:guice-assistedinject:${versions.guice}"
  compile "com.google.inject:guice:${versions.guice}"
  compile 'net.sourceforge.argparse4j:argparse4j:0.4.0'
  compile 'joda-time:joda-time:2.2'

  thrift files('../../thrift/housekeeper.thrift',
      '../../thrift/resource.thrift',
      '../../thrift/scheduler.thrift',
      '../../thrift/root_scheduler.thrift',
      '../../thrift/roles.thrift',
      '../../thrift/flavors.thrift',
      '../../thrift/host.thrift',
      '../../thrift/tracing.thrift',
      '../../thrift/status.thrift')

  testCompile("org.apache.curator:curator-test:${versions.curator}") {
    exclude group: 'org.slf4j', module: 'slf4j-log4j12'
    exclude group: 'log4j', module: 'log4j'
  }

  testCompile 'commons-io:commons-io:2.4'
  testCompile 'org.hamcrest:hamcrest-all:1.3'
  testCompile 'org.testng:testng:6.8'
  testCompile "org.mockito:mockito-core:${versions.mockito}"
  testCompile("org.powermock:powermock-api-mockito:${versions.powermock}") {
    exclude group: 'org.mockito', module: 'mockito-all'
  }
  testCompile "org.powermock:powermock-module-testng:${versions.powermock}"
}

jar {
  manifest {
    attributes 'Specification-Title': 'Photon Controller Housekeeper',
        'Specification-Version': version,
        'Built-By': System.getProperty('user.name'),
        'Built-Date': new Date(),
        'Built-JDK': System.getProperty('java.version'),
        'Built-Gradle': gradle.gradleVersion,
        'Git-Commit': "git rev-parse --short HEAD".execute().text.trim()
  }
}

tasks.withType(Test) {
  scanForTestClasses = false
  include "**/*Test.class"
  exclude "**/helpers/*.class"
}

task extractRelease(dependsOn: distTar) << {
  // Extract housekeeper tar
  def tar = file('build/distributions').findFile(~/housekeeper-.*.tar/)
  def dest = file('build/container/')
  copy {
    from tarTree(tar)
    into dest
  }
  // Rename extracted release directory to make it easy to find from Dockerfile
  def tarOutput = dest.findDirectory(~/housekeeper-.*/).toPath()
  def releaseDir = tarOutput.resolveSibling('release')
  releaseDir.toFile().deleteRecursive()
  Files.move(tarOutput, releaseDir)
}

task dockerfile(type: Dockerfile) {
  dependsOn extractRelease
  destFile = file('build/container/Dockerfile')
  from 'photon/servicebase'
  copyFile('release/', '/usr/lib/esxcloud/housekeeper/')
}

task container(type: DockerBuildImage) {
  dependsOn dockerfile, ':containers:servicebase'
  inputDir = dockerfile.destFile.parentFile
  tag = 'photon/housekeeper'
}

task removeContainer(type: DockerRemoveImage) {
  targetImageId { container.tag }
}

build.dependsOn removeContainer
